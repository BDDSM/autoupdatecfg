/// Считывает параметры доступа на сайт ИТС из файла настроек (its.json)
//
// Возвращаемое значение:
//  Соответствие - Параметры авторизации
//
Функция ПолучитьПараметрыДоступаНаСайтИТС() Экспорт

	КаталогСкрипта = Новый Файл(ТекущийСценарий().Источник).Путь;
	ФайлПараметров = Новый Файл(КаталогСкрипта + "/its.json");

	Если НЕ ФайлПараметров.Существует() Тогда
		ВызватьИсключение "Нет файла настроек для доступа на сайт ИТС (its.json)";
	КонецЕсли;

	ЧтениеТекста = Новый ЧтениеТекста(КаталогСкрипта + "/its.json", "utf-8");
	
	СтрокаJSON = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();
	
	ПараметрыИТС = Новый ПарсерJSON;
	Объект = ПараметрыИТС.ПрочитатьJSON(СтрокаJSON);

	Возврат Объект;
КонецФункции // ПолучитьПараметрыДоступаНаСайтИТС() Экспорт

///	Возвращает структуру с данными о последней версии конфигурации
//
// Параметры:
//  ДанныеКонфигурации - Структура - Информация о тип, версии, версии платформы.
//    Тип - Строка - Тип конфигурации (HRM - ЗУП, Accounting - бухгалтерия ...)
//    Версия - Строка - Версия конфигурации. ЗУП 2.5 имеет версию 25 (убирается точка)
//    ВерсияПлатформы - Строка - Основная платформа для конфигурации, для ЗУП 2.5 версия 82 (для 2 редакций - 82, для 3 - 83)
//
// Возвращаемое значение:
//  Структура - Данные с информацией о релизе
//
Функция ПолучитьИнформацию(ДанныеКонфигурации) Экспорт

	СтрокаЗапроса = "/ipp/ITSREPV/V8Update/Configs/" + ДанныеКонфигурации.Тип 
		+ "/" + ДанныеКонфигурации.Версия 
		+ "/" + ДанныеКонфигурации.ВерсияПлатформы 
		+ "/UpdInfo.txt";

	СерверОбновлений = Новый HTTPСоединение("downloads.1c.ru");
	ЗапросКСерверуОбновлений = Новый HTTPЗапрос(СтрокаЗапроса);
	Ответ = СерверОбновлений.Получить(ЗапросКСерверуОбновлений);

	Если Ответ.КодСостояния <> 200 Тогда
		Возврат Неопределено; 
	КонецЕсли;

	Данные = Ответ.ПолучитьТелоКакСтроку();

	СтруктураРелиза = Новый Структура;
	КолСтрок = СтрЧислоСтрок(Данные)-1;

	КартаКлючей = Новый Соответствие;
	КартаКлючей.Вставить("Version","Версия");
	КартаКлючей.Вставить("FromVersions","ОтВерсий");
	КартаКлючей.Вставить("UpdateDate","ДатаОбновления");

	Для А = 0 По КолСтрок Цикл
		Стр = СтрПолучитьСтроку(Данные, А);

		ПозицияРавно = СтрНайти(Стр,"=");
		Если ПозицияРавно <> 0 Тогда
			Ключ = Сред(Стр,1,ПозицияРавно-1);
			СтруктураРелиза.Вставить(КартаКлючей.Получить(Ключ),Сред(Стр,ПозицияРавно+1));	
		КонецЕсли;
		
	КонецЦикла;

	Возврат СтруктураРелиза;
	
КонецФункции // Получить() Экспорт

///	Возвращает файл обновления до последнего релиза конфигурации
//
// Параметры:
//  Параметры - Структура - Информация о тип, версии, версии платформы.
//    Тип - Строка - Тип конфигурации (HRM - ЗУП, Accounting - бухгалтерия ...)
//    Версия - Строка - Версия конфигурации. ЗУП 2.5 имеет версию 25 (убирается точка)
//    ВерсияПлатформы - Строка - Основная платформа для конфигурации, для ЗУП 2.5 версия 82 (для 2 редакций - 82, для 3 - 83)
//    ПолнаяВерсия - Строка - Полный номер версии конфигурации, например 2.5.105.6
//    Логин - Логин на сайт ИТС
//    Пароль - Пароль на сайт ИТС
//    КаталогСохранения - Каталог сохранения (понимает относительные пути, при необходимости создается)
//
// Возвращаемое значение:
//  Файл - Объект Файл, ссылающийся на скаченный файл
//  Неопределено - в случае ошибки
//
Функция Скачать(Параметры) Экспорт

	Заголовки = Новый Соответствие();
	Заголовки.Вставить("User-Agent", "1C+Enterprise/8.2");

	СерверОбновлений = Новый HTTPСоединение("downloads.v8.1c.ru", , Параметры.Логин, Параметры.Пароль,);
	СтрокаЗапроса = "/tmplts/1c/" + Параметры.Тип + "/" + СокрЛП(СтрЗаменить(Параметры.ПолнаяВерсия, ".", "_")) +"/1cv8.zip";

	ОбеспечитьКаталог(Параметры.КаталогСохранения);

	ЗапросКСерверуОбновлений = Новый HTTPЗапрос(СтрокаЗапроса, Заголовки);
	Ответ = СерверОбновлений.Получить(ЗапросКСерверуОбновлений, Параметры.КаталогСохранения +"\1cv8.zip");

	Если Ответ.КодСостояния = 200 Тогда
		Возврат Новый Файл(Параметры.КаталогСохранения +"\1cv8.zip");
	Иначе
		Возврат Неопределено; 
	КонецЕсли;

КонецФункции // Скачать(ДанныеКонфигурации)

// Вспомогательные методы
// 
Процедура ОбеспечитьКаталог(Знач Каталог)

	Файл = Новый Файл(Каталог);
	Если Не Файл.Существует() Тогда
		СоздатьКаталог(Каталог);
	ИначеЕсли Не Файл.ЭтоКаталог() Тогда
		ВызватьИсключение "Каталог " + Каталог + " не является каталогом";
	КонецЕсли;

КонецПроцедуры